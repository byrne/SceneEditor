<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009" 
					   xmlns:s="library://ns.adobe.com/flex/spark" 
					   xmlns:mx="library://ns.adobe.com/flex/mx"
					   xmlns:mxml="editor.view.mxml.*" 
					   creationComplete="init()"
					   invoke="app_invokeHandler(event)"
					   width="600" height="600"
					   >
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	
	<fx:Script>
		<![CDATA[
			import editor.utils.CommonUtil;
			import editor.utils.FileSerializer;
			import editor.utils.LogUtil;
			import editor.utils.StringUtil;
			import editor.utils.XMLSerializer;
			import editor.utils.swf.CustomEvent;
			import editor.utils.swf.SWF;
			import editor.view.mxml.CustomListItemSkin;
			
			import flashx.textLayout.formats.Direction;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.events.ItemClickEvent;
			
			import spark.events.IndexChangeEvent;
			import spark.events.TextOperationEvent;
			
			private var file:FileReference;
			
			[Bindable]
			private var symbolList:ArrayCollection;
			
			private var swf:SWF;
			
			private function app_invokeHandler(evt:InvokeEvent):void {
//				return;
				var args:Array = evt.arguments;
				if(args.length <= 1) {
					LogUtil.error("usage: SwfReader configFile projDir");
					return;
				}
				var output:Object = new Object();
				var configFileName:String = args[0] as String;
				var proj_dir:String = args[1] as String;
				var config:Object = XMLSerializer.readObjectFromXMLFile(configFileName, "proj_res");// FileSerializer.readJsonFile(configFileName);
				var base_dir:String = config["base_url"];
				output["base_dir"] = base_dir;
				base_dir = base_dir.replace("{proj_dir}", proj_dir);
				
				var output_json:String = config["output_json"];
				output_json = output_json.replace("{proj_dir}", proj_dir);
				
				var output_list:String = config["output_list"];
				output_list = output_list.replace("{proj_dir}", proj_dir);
				
				var files:Array = [];
				appendFileList(files, config["file"]);
				var fileListsArr:Array = config["filelist"] is Array ? config["filelist"] as Array : [config["filelist"]];
				
				var folders:Array = config["directory"] is Array ? config["directory"] : [config["directory"]];
				var baseDir:File = new File(base_dir);
				for each(var list:Object in fileListsArr) {
					appendFileList(files, list["file"], list.base_url);
				} 
				var symbols:Array = [];
				output["symbol_classes"] = new Object();
				for each(var fileName:String in files) {
					fileName = StringUtil.substitute("{0}/{1}", base_dir, fileName);
					dumpSwfSymbolClass(fileName, output, output_json, symbols, output_list, base_dir);
				}
				for each(var item:Object in folders) {
					dumpSwfForFolder(baseDir.resolvePath(item.url), output, output_json, symbols, output_list, base_dir);
				}
				
			}
			
			private static function appendFileList(output:Array, src:*, baseDir:String=null):void {
				var srcArr:Array = src is Array ? src as Array : [src];
				for each(var file:Object in srcArr) {
					output.push(baseDir ? baseDir+"/"+file.url : file.url);
				}
			}
			
			private function dumpSwfForFolder(folder:File,  output:Object, output_json:String, outSymbolsArr:Array, output_list:String, base_dir:String):void {
				if(folder == null || folder.exists == false || folder.isDirectory == false)
					return;
				var content:Array = folder.getDirectoryListing();
				for each(var item:File in content) {
					if(item.isDirectory)
						dumpSwfForFolder(item, output, output_json, outSymbolsArr, output_list, base_dir);
					else if(item.extension == "swf")
						dumpSwfSymbolClass(item.nativePath, output, output_json, outSymbolsArr, output_list, base_dir);
				}
			}
			
			private static var remainSwfCnt:int;
			private function dumpSwfSymbolClass(fileName:String, output:Object, output_json:String, outSymbolsArr:Array, output_list:String, base_dir:String):void {
				remainSwfCnt += 1;
				var ba:ByteArray = FileSerializer.readBytesFromFile(fileName);
				var swf:SWF = new SWF(ba);
//				var base_dir:String = output["base_dir"];
				swf.addEventListener(Event.COMPLETE, function(evt:Event):void {
					var symbols:Array = swf.symbolClassNamesArr;
					fileName = fileName.substr(base_dir.length+1);
					output["symbol_classes"][fileName] = symbols;
					for each(var symbol:String in symbols) {
						outSymbolsArr.push(fileName + " - " + symbol);
					}
					LogUtil.info("dump swf symbol class {0}, count {1}", fileName, symbols.length);
					remainSwfCnt -= 1;
					if(remainSwfCnt <= 0) {
						FileSerializer.writeJsonFile(output, output_json);
						LogUtil.info("write to file: {0}", output_json);
						
						outSymbolsArr.sort();
						FileSerializer.writeListFile(outSymbolsArr, output_list);
						LogUtil.info("write to file: {0}", output_list);
//						exit();
					}
				});
				swf.process();
			}
			
			private function init() :void {
				symbolList = new ArrayCollection();
				this.addEventListener(CustomEvent.EVENT_RECT_CHAGNE, handleRectChange);
			}
			
			private function initList() :void {
				list.dataProvider = symbolList;
				list.addEventListener(ItemClickEvent.ITEM_CLICK, itemClickHandler);
			}
			
			private function handleRectChange(e:CustomEvent) :void {
				var rc:Rectangle = e.data.rect;
				leftTxt.text = rc.left.toFixed(1);
				topTxt.text = rc.top.toFixed(1);
				widthTxt.text = rc.width.toFixed(1);
				heightTxt.text = rc.height.toFixed(1);
			}
			private function onClick():void {
				file = new FileReference();
				file.addEventListener(Event.SELECT, selectHandler);
				
				var allFilter:FileFilter = new FileFilter("swf (*.swf)", "*.swf");
				
				file.browse(new Array(allFilter));
				
			}
			private function selectHandler(event:Event):void {
				file.addEventListener(Event.COMPLETE, onLoadComplete);				
				file.load(); 
			}
			
			private function onLoadComplete(event:Event):void {			
				swf = new SWF(file.data);
				swf.addEventListener(Event.COMPLETE, function(evt:Event):void {
					var symbols:Array = swf.symbolClassNamesArr;
					symbolList = new ArrayCollection();
					for each(var symbol:String in symbols) {
						symbolList.addItem(symbol);
					}
					symbolList.source.sort(Array.CASEINSENSITIVE);
					currentState = "Opened";
				});
				swf.process();
			}
			
			private function getClass(className:String):Class {
				return swf.getClassByName(className);
			}
			
			private function _selectItem(name:String):void {
				var itemClass:Class = swf.getClassByName(name);
				var item:DisplayObject = SWF.buildSymbolInstance(itemClass);
				canvas.show(item);
			}
			private function itemClickHandler(evt:ItemClickEvent):void {				
				_selectItem(String(evt.item));
			}
			
			private function lst_changeHandler(evt:IndexChangeEvent):void {
				var selItems:Vector.<Object> = evt.currentTarget.selectedItems;
				if (selItems.length > 0) {
					_selectItem(String(selItems[0]));
				}
			}
			
			private function filterMyArrayCollection(item:Object):Boolean {
				var searchString:String = searchTxt.text.toLowerCase();
				var itemName:String = (item as String).toLowerCase();
				return itemName.indexOf(searchString) > -1;
			}
			
			protected function searchTxt_changeHandler(event:TextOperationEvent):void
			{
				symbolList.filterFunction = filterMyArrayCollection;
				symbolList.refresh();
			}
		]]>
	</fx:Script>
	
	<s:states>
		<s:State name="beforeOpen"/>
		<s:State name="Opened"/>
	</s:states>
	
	<s:Panel x="0" y="0" width="600" height="600" title="Asset SWF Viewer ">
		<s:Button click="onClick()"  label="Open a Asset SWF..." includeIn="beforeOpen" height="100" width="200" fontSize="15" horizontalCenter="0" verticalCenter="0"/>
		<s:List includeIn="Opened" x="8" y="29" width="154" height="517" id="list"
				itemRenderer="editor.view.mxml.CustomListItemSkin" creationComplete="initList()" change="lst_changeHandler(event)">
		</s:List>
		<s:Scroller  includeIn="Opened" x="170" y="29" percentWidth="100" percentHeight="100"   height="517">
			<s:Group width="418" height="517">
				<mxml:PreviewCanvas id="canvas" >
				</mxml:PreviewCanvas>
			</s:Group>
		</s:Scroller>
		<s:TextInput includeIn="Opened" x="9" y="4" width="152" id="searchTxt" change="searchTxt_changeHandler(event)"/>
		<s:HGroup x="180" y="9" includeIn="Opened" >
			<s:Label   text="Left:"/>
			<s:Label  width="40"  id="leftTxt" text=" "/>
			<s:Label   text="Top:"/>
			<s:Label  width="40" id="topTxt" text=" "/>
			<s:Label   text="Width:"/>
			<s:Label  width="40"  id="widthTxt" text=" "/>
			<s:Label   text="Height:" />
			<s:Label  width="40" id="heightTxt" text=" "/>
		</s:HGroup>
	</s:Panel>
</s:WindowedApplication>
